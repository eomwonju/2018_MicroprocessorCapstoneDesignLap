LIST	P=16F876A

TMR0		EQU		01H
PCL			EQU		02H
STATUS		EQU		03H
PORTA		EQU		05H
PORTB		EQU		06H
PORTC		EQU		07H
TRISA		EQU		85H
TRISB		EQU		86H
TRISC		EQU		87H
ADCON1		EQU		9FH
OPTION_REG	EQU		81H
INTCON		EQU		0BH

;User Register
W_TEMP		EQU		21H
STATUS_TEMP	EQU		22H	
INT_COUNT	EQU		23H		;INTERRUPT NUMBER COUNT
C_1SEC		EQU		24H		;CHECK 1SEC
C_10SEC		EQU		25H		;CHECK 10SEC
C_100SEC	EQU		26H		;CHECK 100SEC
C_1000SEC	EQU		27H		;CHECK 1000SEC
DATA1		EQU		28H
DATA2		EQU		29H
DATA3		EQU		30H
FND1		EQU		31H
FND2		EQU		32H
FND3		EQU		33H
FND4		EQU		34H
NUMBER		EQU		35H

			;START
			ORG		0
			GOTO	START
			
			ORG		04H
			GOTO	ISR
			
ISR			MOVWF	W_TEMP			
			SWAPF	STATUS,W
			MOVWF	STATUS_TEMP
			INCF	INT_COUNT		;INT_COUNT=INT_COUNT+1 IF IN INTERRUPT

			CALL	DISPLAY
			
			SWAPF	STATUS_TEMP,W
			MOVWF	STATUS
			SWAPF	W_TEMP,F
			SWAPF	W_TEMP,W
			BCF		INTCON,2
			RETFIE
;---------------------------------------------	
		
START		;BANK1
			BCF		STATUS,7
			BCF		STATUS,6
			BSF		STATUS,5
			
			;TRISA
			MOVLW	B'11100000'
			MOVWF	TRISA
			;TRISB
			MOVLW	B'11111001'
			MOVWF	TRISB
			;TRIRC
			MOVLW	B'00011000'
			MOVWF	TRISC
			;PORTA DIGITAL
			MOVLW	B'00000111'
			MOVWF	ADCON1
			;OPTION REGISTER
			MOVLW	03H
			MOVWF	OPTION_REG
					
			;BANK0
			BCF		STATUS,7
			BCF		STATUS,6
			BCF		STATUS,5
			;TMR0
			MOVLW	.6
			MOVWF	TMR0
			;INTERRUPT
			BCF		INTCON,5
			BSF		INTCON,7
			;USER REGISTER
			CLRF	INT_COUNT		;INT_COUNT=0
			CLRF	C_1SEC			;C_1SEC=0
			CLRF	C_10SEC			;C_10SEC=0
			CLRF	C_100SEC		;C_100SEC=0
			CLRF	C_1000SEC		;C_100SEC=0

			GOTO	BEGIN

BEGIN		CALL	DISPLAY			; DISPLAY 0000
			BTFSC	PORTB,3			; if we puch start button timer will be start
			GOTO	BEGIN
			BSF		INTCON,5
			GOTO	MAIN
	
MAIN		
			MOVLW	.250
			SUBWF	INT_COUNT,W
			BTFSS	STATUS,2		;STATUS Z FLAG CHECK
			GOTO	SW_CHECK

TIME_CHECK	
T_1SEC		CLRF	INT_COUNT		;FOR NEXT 1SEC
			INCF	C_1SEC
			MOVLW	.10
			SUBWF	C_1SEC,W
			BTFSS	STATUS,2
			GOTO	SW_CHECK
T_10SEC		CLRF	C_1SEC
			INCF	C_10SEC
			MOVLW	.10
			SUBWF	C_10SEC,W
			BTFSS	STATUS,2
			GOTO	SW_CHECK
T_100SEC	CLRF	C_10SEC
			INCF	C_100SEC
			MOVLW	.10
			SUBWF	C_100SEC,W
			BTFSS	STATUS,2
			GOTO	SW_CHECK
T_1000SEC	CLRF	C_100SEC
			INCF	C_1000SEC
			MOVLW	.10
			SUBWF	C_1000SEC,W
			BTFSC	STATUS,2
			CLRF	C_1000SEC
			GOTO	SW_CHECK

SW_CHECK	;SW CHECK  1-START(RB3), 2-STOP(RB4), 3-RESET(RB5)
			BTFSS	PORTB,3
			GOTO	T_START		;TIMER START
			BTFSS	PORTB,4
			GOTO	T_STOP		;TIMER STOP
			BTFSS	PORTB,5
			GOTO	T_RESET1	;TIMER	RESET
			GOTO	MAIN

T_START		BSF		INTCON,5
			GOTO	MAIN

T_STOP		BCF		INTCON,5		;INTERRUPT OFF
			CALL	DISPLAY			;display current timer
			BTFSS	PORTB,3
			GOTO	T_START
			BTFSS	PORTB,5
			GOTO	T_RESET2
			GOTO	T_STOP

T_RESET1	BCF		INTCON,5
			CLRF	INT_COUNT		;INT_COUNT=0
			CLRF	C_1SEC			;C_1SEC=0
			CLRF	C_10SEC			;C_10SEC=0
			CLRF	C_100SEC		;C_100SEC=0
			CLRF	C_1000SEC		;C_100SEC=0
			CALL	DISPLAY
			BCF		PORTA,4			;BUZZER ON
			CALL	DELAY_BZ
			BSF		PORTA,4
			GOTO	T_START

T_RESET2	CLRF	INT_COUNT		;INT_COUNT=0
			CLRF	C_1SEC			;C_1SEC=0
			CLRF	C_10SEC			;C_10SEC=0
			CLRF	C_100SEC		;C_100SEC=0
			CLRF	C_1000SEC		;C_100SEC=0
			CALL	DISPLAY
			BCF		PORTA,4			;BUZZER ON
			CALL	DELAY_BZ
			BSF		PORTA,4
			GOTO	T_STOP

;---------------------------------------------FUNCTION
DISPLAY		
			MOVF	C_1SEC,W
			CALL	FNDCHANGE
			MOVWF	FND4

			MOVF	C_10SEC,W
			CALL	FNDCHANGE
			MOVWF	FND3

			MOVF	C_100SEC,W
			CALL	FNDCHANGE
			MOVWF	FND2

			MOVF	C_1000SEC,W
			CALL	FNDCHANGE
			MOVWF	FND1
			
			;DISPLAY CHAING CC (common cathode)
			MOVF	FND4,W
			CALL	SHOW
			BCF		PORTB,1
			CALL	DELAY_DS
			BSF		PORTB,1

			MOVF	FND3,W
			CALL	SHOW
			BCF		PORTB,2
			CALL	DELAY_DS
			BSF		PORTB,2
	
			MOVF	FND2,W
			CALL	SHOW
			BCF		PORTA,2
			CALL	DELAY_DS
			BSF		PORTA,2

			MOVF	FND1,W
			CALL	SHOW
			BCF		PORTA,3
			CALL	DELAY_DS
			BSF		PORTA,3
	
			RETURN

;---------------------------------------------
DELAY_BZ	;BUZZER DELAY
			MOVLW	.255
			MOVWF	DATA1
D1			MOVLW	.255
			MOVWF	DATA2
D2			DECFSZ	DATA2
			GOTO	D2
			DECFSZ	DATA1
			GOTO	D1

			RETURN
;---------------------------------------------
DELAY_DS	;DISPLAY DELAY
			MOVLW	.25
			MOVWF	DATA1
DD1			MOVLW	.10
			MOVWF	DATA2
DD2			DECFSZ	DATA2
			GOTO	DD2
			DECFSZ	DATA1
			GOTO	DD1

			RETURN
;---------------------------------------------
FNDCHANGE	
			ANDLW	 0FH
			ADDWF	 PCL,F
			RETLW	 B'11111100'	;0
			RETLW	 B'01100000'	;1
			RETLW	 B'11011010'	;2
			RETLW	 B'11110010'	;3
			RETLW	 B'01100110'	;4
			RETLW	 B'10110110'	;5
			RETLW	 B'10111110'	;6
			RETLW	 B'11100100'	;7
			RETLW	 B'11111110'	;8
			RETLW	 B'11100110'	;9

;---------------------------------------------
SHOW		;NUMBER = 'abcdefgh'
			MOVWF	NUMBER

			BTFSS	NUMBER,7
			BCF		PORTC,7
			BTFSC	NUMBER,7
			BSF		PORTC,7

			BTFSS	NUMBER,6
			BCF		PORTC,6
			BTFSC	NUMBER,6
			BSF		PORTC,6

			BTFSS	NUMBER,5
			BCF		PORTC,5
			BTFSC	NUMBER,5
			BSF		PORTC,5

			BTFSS	NUMBER,4
			BCF		PORTC,1
			BTFSC	NUMBER,4
			BSF		PORTC,1

			BTFSS	NUMBER,3
			BCF		PORTC,0
			BTFSC	NUMBER,3
			BSF		PORTC,0

			BTFSS	NUMBER,2
			BCF		PORTC,2
			BTFSC	NUMBER,2
			BSF		PORTC,2

			BTFSS	NUMBER,1
			BCF		PORTA,1
			BTFSC	NUMBER,1
			BSF		PORTA,1

			BTFSS	NUMBER,0
			BCF		PORTA,0
			BTFSC	NUMBER,0
			BSF		PORTA,0

			RETURN
;---------------------------------------------
			END